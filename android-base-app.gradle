import com.architecture.Dependencies as deps

apply plugin: deps.BuildPlugins.androidApplication
apply plugin: deps.BuildPlugins.kotlinAndroid
apply plugin: deps.BuildPlugins.kapt

android {

    compileSdkVersion(deps.Android.compileSdkVersion)
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId = deps.Android.applicationId
        minSdkVersion deps.Android.minSdkVersion
        targetSdkVersion deps.Android.targetSdkVersion
        versionCode = deps.Android.versionCode
        versionName = deps.Android.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {

        release {

            def props = new Properties()
            def fileInputStream = new FileInputStream(file('keystore/sample.release.properties'))
            props.load(fileInputStream)
            fileInputStream.close()

            storeFile = file(props['storeFile'])
            storePassword = props['storePassword']
            keyAlias = props['keyAlias']
            keyPassword = props['keyPassword']

            v1SigningEnabled true
            v2SigningEnabled true
        }

        debug {
            storeFile file('keystore/sample.debug.keystore')
            keyAlias 'androiddebugkey'
            storePassword 'android'
            keyPassword 'android'

            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            resValue 'string', 'app_name', '"SampleArchitecture"'
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
//            signingConfig signingConfigs.release
        }

        debug {
            resValue 'string', 'app_name', '"SampleArchitecture-dev"'
            minifyEnabled false
            shrinkResources false
//            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "version"

    productFlavors {

        dev {
            dimension "version"
            applicationIdSuffix ".dev"
            buildConfigField("String", "BASE_URL", "\"http://www.mocky.io/v2/\"")
        }

        prod {
            dimension "version"
            buildConfigField("String", "BASE_URL", "\"http://www.mocky.io/v2/\"")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    buildFeatures {

        // Determines whether to generate a BuildConfig class.
//        buildConfig = true

        // Determines whether to support View Binding
        viewBinding = true
    }
}

dependencies {
    implementation(deps.kotlinStdLib)
    implementation(deps.AndroidX.appcompat)
    implementation(deps.AndroidX.coreKtx)
    implementation(deps.AndroidX.constraintLayout)
}
